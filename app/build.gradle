plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.github.spotbugs' version '4.7.1'
    id 'com.adarshr.test-logger' version '3.0.0'
    id 'org.jetbrains.dokka' version '1.4.32'
}

import org.ajoberstar.grgit.Grgit

ext {
    git = Grgit.open(currentDir: projectDir)
    gitVersionName = git.describe()
    gitVersionCode = git.log().size()
}

android {
    compileSdkVersion 31
    buildToolsVersion '31.0.0'
    signingConfigs {
        release {
            storeFile file('../keystore/keystore-release.jks')
            storePassword 'android'
            keyAlias 'developmentkey'
            keyPassword 'android'
        }
    }

    defaultConfig {
        applicationId "cz.fontan.gomoku_gui"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode gitVersionCode
        versionName gitVersionName
        buildConfigField 'String', 'BUILD_TIME', 'new java.text.SimpleDateFormat("yyyy-MM-dd\'T\'HH:mm:ssZ", java.util.Locale.getDefault()).format(new java.util.Date(' + System.currentTimeMillis() + 'L))'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags '-std=c++17'
                arguments "-DANDROID_TOOLCHAIN=clang", "-DCPU_PLATFORM=Modern"
            }
        }
    }

    spotbugs {
        excludeFilter = file("$rootDir/gradle/config/excludeFilter.xml")
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "versionName", gitVersionName
    }

    buildTypes {
        debug {
            versionNameSuffix ".Debug"
            debuggable true
            jniDebuggable true
            minifyEnabled false
            signingConfig signingConfigs.debug
            externalNativeBuild {
                cmake {
                    arguments "-DCMAKE_CXX_FLAGS_DEBUG=-O0 -DCMAKE_BUILD_TYPE=Debug"
                }
            }
        }
        release {
            debuggable false
            jniDebuggable false
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            renderscriptOptimLevel 3
            externalNativeBuild {
                cmake {
                    arguments "-DCMAKE_CXX_FLAGS_SPEEDY=-Ofast -DCMAKE_BUILD_TYPE=Speedy"
                }
            }
        }
    }

    flavorDimensions "version", "gametype"
    productFlavors {
        free {
            dimension "version"
            versionNameSuffix ".Free"
            externalNativeBuild {
                cmake {
                    arguments "-DSPECIAL_TARGET=Gomocup"
                }
            }
        }
        profi {
            dimension "version"
            versionNameSuffix ".Profi"
            externalNativeBuild {
                cmake {
                    arguments "-DSPECIAL_TARGET=Mercury"
                }
            }
        }

        caro {
            dimension 'gametype'
            applicationIdSuffix ".caro"
            versionNameSuffix ".Caro"
            externalNativeBuild {
                cmake {
                    arguments "-DGAME_TYPE=Caro"
                }
            }
        }
        freestyle {
            dimension 'gametype'
            applicationIdSuffix ".freeStyle"
            versionNameSuffix ".FreeStyle"
            externalNativeBuild {
                cmake {
                    arguments "-DGAME_TYPE=FreeStyle"
                }
            }
        }
        renju {
            dimension 'gametype'
            applicationIdSuffix ".renju"
            versionNameSuffix ".Renju"
            externalNativeBuild {
                cmake {
                    arguments "-DGAME_TYPE=Renju"
                }
            }
        }
        standard {
            dimension 'gametype'
            applicationIdSuffix ".standard"
            versionNameSuffix ".Standard"
            externalNativeBuild {
                cmake {
                    arguments "-DGAME_TYPE=Standard"
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility "11"
        targetCompatibility "11"
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.10.2'
        }
    }
    buildFeatures {
        viewBinding true
    }
    ndkVersion '21.4.7075529'
    sourceSets {
        renju {
            res {
                srcDirs 'src\\renju\\res'
            }
        }
        caro {
            res {
                srcDirs 'src\\caro\\res'
            }
        }
        freestyle {
            res {
                srcDirs 'src\\freestyle\\res'
            }
        }
        standard {
            res {
                srcDirs 'src\\standard\\res'
            }
        }
    }
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.6.10'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.test.espresso:espresso-idling-resource:3.4.0'
    implementation 'androidx.core:core-splashscreen:1.0.0-alpha02'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-idling-resource:3.4.0'
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.11.0'
}
