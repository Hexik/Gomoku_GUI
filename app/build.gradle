plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id("com.github.spotbugs") version "4.7.1"
    id 'com.adarshr.test-logger' version '3.0.0'
    id("org.jetbrains.dokka") version "1.4.32"
}

import org.ajoberstar.grgit.Grgit

ext {
    git = Grgit.open(currentDir: projectDir)
    gitVersionName = git.describe()
    gitVersionCode = git.log().size()
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"
    signingConfigs {
        release {
            storeFile file('../keystore/keystore-release.jks')
            storePassword 'android'
            keyAlias 'developmentkey'
            keyPassword 'android'
        }
    }

    defaultConfig {
        applicationId "cz.fontan.gomoku_gui"
        minSdkVersion 19
        targetSdkVersion 30
        versionCode gitVersionCode
        versionName gitVersionName
        buildConfigField 'String', 'BUILD_TIME', 'new java.text.SimpleDateFormat("yyyy-MM-dd\'T\'HH:mm:ssZ", java.util.Locale.getDefault()).format(new java.util.Date(' + System.currentTimeMillis() + 'L))'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags '-std=c++17'
                arguments "-DANDROID_TOOLCHAIN=clang"
//                arguments "-DANDROID_ARM_MODE=arm", "-DANDROID_STL=c++_shared"
            }
        }
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "versionName", gitVersionName
    }

    buildTypes {
        debug {
            versionNameSuffix ".Debug"
            debuggable true
            jniDebuggable true
            minifyEnabled false
            signingConfig signingConfigs.debug
            externalNativeBuild {
                cmake {
                    arguments "-DCMAKE_CXX_FLAGS_DEBUG=-O0 -DCMAKE_BUILD_TYPE=Debug"
                }
            }

        }
        release {
            versionNameSuffix ".Release"
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            renderscriptOptimLevel 3
            externalNativeBuild {
                cmake {
                    arguments "-DCMAKE_CXX_FLAGS_RELEASE=-O1 -DCMAKE_BUILD_TYPE=Release"
                }
            }
        }
        speedy {
            initWith(getByName("release"))
            versionNameSuffix ".Speedy"
            externalNativeBuild {
                cmake {
                    arguments "-DCMAKE_CXX_FLAGS_SPEEDY=-Ofast -DCMAKE_BUILD_TYPE=Speedy"
                }
            }
        }
    }

    flavorDimensions "version", "gametype"
    productFlavors {
        free {
            dimension "version"
            versionNameSuffix ".Free"
        }
        profi {
            dimension "version"
            versionNameSuffix ".Profi"
        }
        caro {
            dimension 'gametype'
            versionNameSuffix ".Caro"
            externalNativeBuild {
                cmake {
                    arguments "-DGAME_TYPE=Caro"
                }
            }
        }
        freestyle {
            dimension 'gametype'
            versionNameSuffix ".FreeStyle"
            externalNativeBuild {
                cmake {
                    arguments "-DGAME_TYPE=FreeStyle"
                }
            }
        }
        renju {
            dimension 'gametype'
            versionNameSuffix ".Renju"
            externalNativeBuild {
                cmake {
                    arguments "-DGAME_TYPE=Renju"
                }
            }
        }
        standard {
            dimension 'gametype'
            versionNameSuffix ".Standard"
            externalNativeBuild {
                cmake {
                    arguments "-DGAME_TYPE=Standard"
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility "1.8"
        targetCompatibility "1.8"
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.10.2'
        }
    }
    buildFeatures {
        viewBinding true
    }
    ndkVersion '22.1.7171670'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.5.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.1"
    implementation 'androidx.preference:preference-ktx:1.1.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.11.0'
}
